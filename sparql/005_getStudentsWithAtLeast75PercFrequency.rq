PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX att: <http://www.semanticweb.org/part-time-team/ontologies/2023/5/attendance-ontology#>
PREFIX vcard: <http://www.w3.org/2006/vcard/ns#>

# Students having a presence frequency at DA_WebSemantico higher (or equal) to 75%
SELECT ?student ?percentage WHERE {
    # Retrieve frequency from each student
    {
        SELECT ?wrk ?student (count(?attendance) AS ?tot_freq) WHERE {
            ?wrk att:hasClass ?class .
            ?class att:hasClassStudent ?student .

            OPTIONAL {
                ?wrk att:hasLesson ?lesson .
                ?lesson att:hasPin ?pin .
                ?pin att:hasAttendance ?attendance .
                ?attendance att:hasAttendant ?student ;
                    rdf:type att:AttendanceValid .
            }
        }

        GROUP BY ?wrk ?student
    }
    # Retrieve the number of attendanble lessons
    {
        SELECT ?wrk (count(?lesson) AS ?tot) WHERE {
            ?wrk att:hasLesson ?lesson .
        }

        GROUP BY ?wrk
    }

    # Get only student with more than 75% of attendend lessons.
    BIND ((?tot_freq / ?tot * 100) AS ?percentage)
    FILTER (?wrk = att:WRK_CL_001_DA_ProjectManagement_2023)
	# FILTER (?percentage >= 75)
}