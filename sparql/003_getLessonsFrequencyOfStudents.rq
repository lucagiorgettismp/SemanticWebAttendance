PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX att: <http://www.semanticweb.org/part-time-team/ontologies/2023/5/attendance-ontology#>
PREFIX vcard: <http://www.w3.org/2006/vcard/ns#>

# DA_WebSemantico attendance frequency
SELECT ?student ((?tot_freq / ?tot * 100) AS ?percentage) WHERE {
	{
		SELECT ?student (count(?attendance) AS ?tot_freq) WHERE {
			?wrk att:hasClass ?class .
			?class att:hasStudent ?student .

			OPTIONAL {
				?wrk att:hasLesson ?lesson .
				?lesson att:hasPin ?pin .
				?pin att:hasAttendance ?attendance .
				?attendance att:hasAttendant ?student ;
					rdf:type att:AttendanceValid .
			}
			FILTER (?wrk = att:WRK_CL_001_DA_WebSemantico_2023)
		}

		GROUP BY ?student
	}
	{
		SELECT (count(?lesson) AS ?tot) WHERE {
			?wrk att:hasLesson ?lesson .
			FILTER (?wrk = att:WRK_CL_001_DA_WebSemantico_2023)
		}
	}
}

GROUP BY ?student ?tot ?tot_freq
ORDER BY ?student
