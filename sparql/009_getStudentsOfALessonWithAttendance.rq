PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX att: <http://www.semanticweb.org/part-time-team/ontologies/2023/5/attendance-ontology#>
PREFIX vcard: <http://www.w3.org/2006/vcard/ns#>

# Students of the specified lesson and their attendance.
SELECT DISTINCT ?student ?type WHERE {
    ?lesson att:hasStudent ?student . 

    OPTIONAL { 
        ?lesson att:hasPin ?pin .
        ?pin att:hasAttendance ?attendance .
        ?attendance att:hasAttendant ?student ;
            rdf:type ?type .
        {        
            ?type rdfs:subClassOf att:AttendanceValid .
        }
        UNION
        {        
            ?type rdfs:subClassOf att:AttendanceNotValid .
        }
    }

    FILTER (?lesson = att:LES_WS_2023_05_22)
}

ORDER BY ?student